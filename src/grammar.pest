/// Matches any amount of whitespace (spaces, tabs, newlines)
WHITESPACE = _{" " | "\t" | "\r" | "\n"}

/// Matches the `CREATE` keyword
CREATE = {"CREATE" | "create"}
/// Matches the `TABLE` keyword
TABLE = {"TABLE" | "table"}

/// Matches the `NOT` keyword
NOT = {"NOT" | "not"}
/// Matches the `NULL` keyword
NULL = {"NULL" | "null"}

/// Matches an identifier, such as table or column names
identifier = @{(ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")*}

/// Matches numeric literals (used for VARCHAR length)
number = @{ASCII_DIGIT+}

// Data Type Rules
/// Matches a simple data type without parameters (e.g., INT, TEXT, BOOLEAN)
simple_type = @{"INT" | "int" | "TEXT" | "text" | "BOOLEAN" | "boolean" | "DATE" | "date"}

/// Matches the VARCHAR data type with a specified length, e.g., VARCHAR(255)
varchar_type = {("VARCHAR" | "varchar") ~ "(" ~ number ~ ")"}

/// Matches any supported data type
data_type = {simple_type | varchar_type}

/// Matches the "NOT NULL" constraint
not_null_constraint = {(NOT ~ NULL)}

// Column Definition Rules
/// Matches a single column definition, e.g., "user_id INT NOT NULL"
column_definition = {identifier ~ data_type ~ not_null_constraint?}

/// Matches a comma-separated list of column definitions
column_list = {column_definition ~ ("," ~ column_definition)*}

// Main Query Rule
/// Matches an entire `CREATE TABLE` query
create_query = {SOI ~ CREATE ~ TABLE ~ identifier ~ "(" ~ column_list ~ ")" ~ (";")? ~ EOI}